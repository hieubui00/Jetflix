apply plugin: 'com.android.application'

apply from: "$rootDir/builder.gradle"

android {
    namespace 'com.hieubui.jetflix'

    defaultConfig {
        applicationId 'com.hieubui.jetflix'

        buildConfigField "String", "API_HOST", String.format("\"%s\"", API_HOST)
        buildConfigField "String", "API_KEY", String.format("\"%s\"", API_KEY)
    }

    buildFeatures {
        compose true
    }

    flavorDimensions 'default'
    productFlavors {
        develop {
            applicationIdSuffix '.develop'
            versionCode 1
            versionName '1.0.0'

            resValue 'string', 'app_name', 'Jetflix Develop'
            dimension 'default'
        }

        production {
            versionCode 1
            versionName '1.0.0'

            resValue 'string', 'app_name', 'Jetflix'
            dimension 'default'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.3.1'
    }

    dynamicFeatures = [':features:home']
}

dependencies {
    implementation project(':core')
    implementation project(':resources')

    api(
        libs.composeLiveData,
        libs.lifecycleRuntime,
        libs.navigationFragment,
        libs.navigationUI,
        libs.navigationDynamicFeatures,
        libs.dagger,
        libs.coil
    )

    implementation(
        libs.okHTTP,
        libs.retrofit,
        libs.converterGSON
    )

    implementation(libs.loggingInterceptor) {
        exclude group: 'org.json', module: 'json'
    }

    debugImplementation(debugLibs.composeTooling)

    kapt(kaptLibs.daggerCompiler)
}